Pro pltqbizc,ps=ps
; Program pltqbizc
; This program generates a contour plot of the characteristic exponent
; q (in the equatorial plane) generated by the program TPMapc.f.
; It is based on the IDL program pltftvc.
; Version 2
; This version incorporates the non-uniform grid in rho first 
; instituted in Versions 2 and 2b of Initmapc.  It also utilizes the
; new definition of the magnetopause boundary crossing.
; by Fred Hall, IV and Antonius Otto
; 19 September 2000

; Initialize some variables
; Note that IDL uses long (l) integers with unformatted data.
rhomin=-40.0
rhomin2=rhomin
rhomax=15.0
rhomax2=rhomax
nrho=110l
nrho2=nrho
phimin=-25.0
phimin2=phimin
phimax=25.0
phimax2=phimax
nphi=100l
nphi2=nphi
YEAR=1980l
YEAR2=YEAR
IDAY=50l
IDAY2=IDAY
UTHOUR=21l
UTHOUR2=UTHOUR
MINUTE=0l
MINUTE2=MINUTE
SECOND=0l
SECOND2=SECOND
IOPT=1l
IOPT2=IOPT
psub=0.0
continue='p'
totcross=500
pi=3.141592654
mismatch=0
tgchoice='n'
Tsyftvinit=0.0
noplt=1.7e5
critvar=5.0

PARMOD=fltarr(4)
PARMOD2=PARMOD


; Open data file and obtain preliminary parameter data.
openr,4,'tpmvdiagc.bin',/F77_UNFORMATTED
readu,4,rhomin
readu,4,rhomax
readu,4,nrho
readu,4,phimin
readu,4,phimax
readu,4,nphi
readu,4,YEAR
readu,4,IDAY
readu,4,UTHOUR
readu,4,MINUTE
readu,4,SECOND
for n=1,4 do begin
  readu,4,psub
  PARMOD(n-1)=psub
endfor
readu,4,IOPT
readu,4,critfvr


; Define coordinate and data matrices
rho=findgen(nrho+1)
kappa=alog(rhomax/rhomin)/nrho
rho=rhomin*exp(kappa*rho)
rho(0)=rhomin
rho(nrho)=rhomax
phi=findgen(nphi+1)
dphi=(phimax-phimin)/nphi
phi=phi*dphi+phimin
tacrho=phi
fvr=fltarr(nrho+1,nphi+1,/NOZERO)
qbiz=fvr

; Read in the data
readu,4,fvr
readu,4,qbiz

close,4



; Open data file and obtain preliminary parameter data.
openr,5,'tpmmcc2.bin',/F77_UNFORMATTED
readu,5,rhomin2
if (rhomin2 NE rhomin) then mismatch=1
readu,5,rhomax2
if (rhomax2 NE rhomax) then mismatch=1
readu,5,nrho2
if (nrho2 NE nrho) then mismatch=1
readu,5,phimin2
if (phimin2 NE phimin) then mismatch=1
readu,5,phimax2
if (phimax2 NE phimax) then mismatch=1
readu,5,nphi2
if (nphi2 NE nphi) then mismatch=1
readu,5,YEAR2
if (YEAR2 NE YEAR) then mismatch=1
readu,5,IDAY2
if (IDAY2 NE IDAY) then mismatch=1
readu,5,UTHOUR2
if (UTHOUR2 NE UTHOUR) then mismatch=1
readu,5,MINUTE2
if (MINUTE2 NE MINUTE) then mismatch=1
readu,5,SECOND2
if (SECOND2 NE SECOND) then mismatch=1
for n=1,4 do begin
  readu,5,psub
  PARMOD2(n-1)=psub
  if (PARMOD2(n-1) NE PARMOD(n-1)) then mismatch=1
endfor
readu,5,IOPT2
if (IOPT2 NE IOPT) then mismatch=1

if (mismatch EQ 1) then print, 'A data mismatch has been detected.'

readu,5,totcross
print,'totcross = ',totcross

; Define additional data matrices
stcross=lonarr(nphi+1)
mpccrho=fltarr(totcross)
mpccphi=mpccrho

; Read in the data
readu,5,stcross

close,5


; Obtain the actual magnetopause crossings
mpcounter=0
print,'nrho = ',nrho
print,'nphi = ',nphi
for kmpc=0,nphi-1 do $
  if (stcross(kmpc) ne nrho) then begin $
    print,'stcross = ',stcross(kmpc),$
          '(',kmpc,'):   mpcounter = ',mpcounter         
    mpccrho(mpcounter)=rho(stcross(kmpc))
    mpccphi(mpcounter)=phi(kmpc)
    mpcounter=mpcounter+1
  endif
print
print
print
print,'Magnetopause crossings:'
print,'mpccrho:'
print,mpccrho
print
print,'mpccphi:'
print,mpccphi
print
print
print



; Clip the plot
for i=0,nrho do begin
  for j=0,nphi do begin
    if (fvr(i,j) eq -499.9) then begin
      fvr(i,j) = -20.0
      qbiz(i,j)=-20.0
    endif
    if (fvr(i,j) eq -599.9) then begin
      fvr(i,j)=-10.0
      qbiz(i,j)=-10.0
    endif
  endfor
endfor


; Enable printer if specified
if keyword_set(ps) then begin
  set_plot,'ps'
  device,/portrait
  device,/inches,xsize=6.0,ysize=5.5,xoffset=1.0,yoffset=4.0
  device,filename='pltqbizc.ps'
endif

; Generate the plot

polar_contour,transpose(qbiz),phi,rho, $
; /T3D,/NOERASE,/SAVE $
  TITLE='Tsyganenko T96_01 Characteristic Exponent q', $
  XTITLE='x (R!DE!N)',YTITLE='y (R!DE!N)', $
  XRANGE=[-45,15],YRANGE=[-30,30], $
  XSTYLE=1,YSTYLE=1, $
  LEVELS=[-10.0,-5.0,-1.0,1.0,5.0,10.0,100.0], $
  C_LABELS=[1,0,1,1,0,1,1], $
  C_LINESTYLE=[1,1,1,0,0,0,0]
; ZVALUE=1.0,MAX_VAL=20


oplot,mpccrho,mpccphi,/POLAR,PSYM=2,THICK=4

; Overlay tactical grid
print,'nrho = ',nrho
print,'rho(0) = ',rho(0)
print,'rho(nrho) = ', rho(nrho)
print
print,'nphi = ',nphi
print,'phi(0) = ',phi(0)
print,'phi(nphi) = ',phi(nphi)
print,'Would you like a tactical grid overlay of the plot? (y/n)'
repeat begin
  read,tgchoice
endrep until ((tgchoice EQ 'y') OR (tgchoice EQ 'n'))
if (tgchoice EQ 'y') then begin
  for j=0,nphi do begin
    oplot,[rho(0),rho(nrho)],[phi(j),phi(j)],/POLAR,LINESTYLE=0
  endfor
  for i=0,nrho do begin
    tacrho(*)=rho(i)
    oplot,tacrho,phi,/POLAR,LINESTYLE=0
  endfor
endif


print,max(qbiz)
print,min(qbiz)
print,min(abs(qbiz))


; Close printer device if opened and reset normal plotting
if keyword_set(ps) then begin
  device,/close
  set_plot,'x'
endif

end
